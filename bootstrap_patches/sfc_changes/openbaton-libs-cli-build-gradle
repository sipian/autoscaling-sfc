
/*
 * Copyright (c) 2016 Open Baton (http://www.openbaton.org)
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 */


//#### debian ####

plugins {
    id "nebula.os-package" version "2.2.6"
}
apply from: 'gradle/gradle/command.line.helper.gradle'
apply from: 'gradle/gradle/git.helpers.gradle'

project.ext {
    //------------------------------------------------//
    /**
     * Application Related Version
     */
    applicationVersion = version

    applicationRelease = isApplicationRelease()
    applicationDate = new Date()
    applicationRevision = getRevision()

    applicationVersionSnapshot = (!applicationRelease) ? ".${asUTC(applicationDate, 'yyyyMMdd')}.git${applicationRevision}" : ""
    applicationVersionFull = "${applicationVersion}${applicationVersionSnapshot}"
    //------------------------------------------------//
}

apply from: 'gradle/gradle/packaging.vimplugin.debian.gradle'
apply from: 'gradle/gradle/distribution.gradle'
//################


repositories {
    mavenLocal()
    mavenCentral()
}
dependencies {
    compile 'org.openbaton:catalogue:3.9.9'
    compile project(":sdk")
    compile 'org.slf4j:slf4j-log4j12:1.7.12'
    compile 'org.apache.commons:commons-lang3:3.0'
    compile 'jline:jline:2.12.1'
}

jar {
    manifest {
        attributes 'Main-Class': 'org.openbaton.cli.NFVOCommandLineInterface'
    }
}

task fatJar(type: Jar) {
    manifest {
        attributes 'Implementation-Title': 'NFVOCommandLineInterface',
                'Implementation-Version': version,
                'Main-Class': 'org.openbaton.cli.NFVOCommandLineInterface'
    }
    baseName = project.name + '-all'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

jar.dependsOn fatJar

